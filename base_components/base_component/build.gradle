apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
android {
    //给 Module 内的资源名增加前缀, 避免资源名冲突
//    resourcePrefix "${project.name.toLowerCase().replaceAll("-", "_")}_"

    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        //Arouter
        /**
         * ARouter:path必须以'/'开头，并且包含多于2 '/'的内容!
         * ARouter:默认会用你R申明的path的第一个/后面的字符用作group，不同组件之间的group不能相同
         */
        kapt {
            arguments {
                println("jay-" + project.getName())
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes.each {

        Properties props
        if (it.name == "debug") {
            props = rootProject.ext.environment['debugEnvironmentProps']
        } else {
            props = rootProject.ext.environment['liveEnvironmentProps']
        }

        it.buildConfigField("String", "BASE_URL", String.format(Locale.getDefault(), "\"%s\"", props["base_url"]))

    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    /**
     * 基础功能组件
     */
    api project(path: ':base_libs:base_lib')
//    api 'com.jaydroid.base_lib:base_lib:1.0.0'

    //初始的依赖
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.dependencies['kotlin-stdlib-jdk7']
    implementation rootProject.ext.dependencies['appcompat']
    implementation rootProject.ext.dependencies['core-ktx']
    implementation rootProject.ext.dependencies['constraintlayout']
    testImplementation rootProject.ext.dependencies['junit']
    androidTestImplementation rootProject.ext.dependencies['junit-androidx']
    androidTestImplementation rootProject.ext.dependencies['espresso-core-androidx']

    api rootProject.ext.dependencies['eventbus']
    api rootProject.ext.dependencies['persistentcookiejar']
    api rootProject.ext.dependencies['statusbar-util']
    api rootProject.ext.dependencies['butterknife']
    kapt rootProject.ext.dependencies['butterknife-compiler']
    //其它依赖
    //arouter-compiler 的注解依赖需要所有使用 ARouter 的 model 都添加依赖
    kapt rootProject.ext.dependencies['arouter-compiler']

}

apply plugin: 'maven'
uploadArchives {
    //dependencies path: com.jaydroid.base_component:base_component:1.0.0
    repositories.mavenDeployer {
        // 本地路径
        repository(url: MAVEN_LOCAL_REPO_PATH)
        // 唯一标识
        pom.groupId = "com.jaydroid.base_component"
        // 项目名称
        pom.artifactId = "base_component"
        // 版本号
        pom.version = "1.0.0"
    }
}
